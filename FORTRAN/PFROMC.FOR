      PROGRAM FNDTEN
      COMMON HB,HCRIT,TEMP,APH30,BPH60,APCO2,BPCO2,DP50,PB,PIO2
      DIMENSION PI(5)
      NSET=0
  1   CONTINUE
      NSET=NSET+1
      APCO2=30.0
      BPCO2=60.0
      WRITE(*,2)NSET
  2   FORMAT(' ENTER [O2] AND [CO2], ml/dl FOR SET #',I5)
      READ(*,*)ARTO2C,ARTCO2
      WRITE(*,3)
  3   FORMAT(' ENTER HB,HCRIT,TEMP,APH30,BPH60,P50,PB,PIO2')
      READ(*,*)HB,HCRIT,TEMP,APH30,BPH60,P50,PB,PIO2
      DP50=P50-26.8
      PI(1)=10.0
      PI(2)=1.0
      PI(3)=0.1
      PI(4)=0.01
      PI(5)=0.001
      PPCO=0.0
      DO 20 K=1,5
  10  PPCO=PPCO+PI(K)
      CALL SAMEO2 (PPO,PPCO,ARTO2C,CO2CT2)
      IF(CO2CT2-ARTCO2) 10,20,20
  20  PPCO=PPCO-PI(K)
      WRITE(*,25)NSET
      WRITE(3,25)NSET
  25  FORMAT(' SET NUMBER =',I5,/)
      WRITE(*,30)PPO,PPCO,ARTO2C,CO2CT2
      WRITE(3,30)PPO,PPCO,ARTO2C,CO2CT2
  30  FORMAT('  PO2 =',F8.1,'     PCO2  =',F8.1,/,' [O2] =',F10.3,
     1'   [CO2] =',F10.3)
      WRITE(*,40)
  40  FORMAT(' MORE DATA SETS? (YES=1; 0=NO)')
      READ(*,*)IMOR
      IF(IMOR.NE.0) GO TO 1
      END
      SUBROUTINE SAMEO2(PPO,PPCO,ARTO2C,CO2CT2)
      COMMON HB,HCRIT,TEMP,APH30,BPH60,APCO2,BPCO2,DP50,PB,PIO2
      E=0.0
      F=PIO2+10.0
  10  G=(E+F)/2.0
      CALL BLOOD(G,PPCO,O2CNT2,CO2CT2)
      A10=ABS(O2CNT2-ARTO2C)
      IF(A10-0.0001) 50,50,20
  20  IF(O2CNT2-ARTO2C) 30,50,40 
  30  E=G
      GO TO 10 
  40  F=G
      GO TO 10 
  50  CONTINUE
      PPO=G
      RETURN
      END
      SUBROUTINE BLOOD(PO2,PCO2,O2C,CO2C)
      COMMON HB,HCRIT,TEMP,APH30,BPH60,APCO2,BPCO2,DP50,PB,PIO2
      PH1=PH(PCO2,0.0)
      Y=0.003*HB*(1.0-SATURA(PO2,PCO2,PH1)/100.0)
      PH2=PH(PCO2,Y)
      SATRN=SATURA(PO2,PCO2,PH2)
      O2C=0.0139*HB*SATRN + 0.003*PO2
      CO2C=CO2CON(PCO2,PH2,SATRN)
      RETURN
      END
      FUNCTION PH(PCO2,Y)
      COMMON HB,HCRIT,TEMP,APH30,BPH60,APCO2,BPCO2,DP50,PB,PIO2
      IF(PCO2.LE.0.001) PCO2=0.001
      IF(APH30-1.0) 10,10,20
  10  PH=7.59 + Y - 0.2741*ALOG(PCO2/20.0)
      RETURN  
  20  PH=BPH60+Y+(APH30-BPH60)*ALOG(PCO2/BPCO2)/ALOG(APCO2/BPCO2)
      RETURN
      END
      FUNCTION CO2CON(PCO2,PHE,SATN)
      COMMON HB,HCRIT,TEMP,APH30,BPH60,APCO2,BPCO2,DP50,PB,PIO2
      P=7.4-PHE
      PK=6.086+0.042*P+(38.0-TEMP)*(0.00472+0.00139*P)
      SOL=0.0307 + 0.00057*(37.0-TEMP) + 0.00002*(37.0-TEMP)*(37.0-TEMP)
      DOX=0.59+0.2913*P-0.0844*P*P
      DR=0.664+0.2275*P-0.0938*P*P
      DDD=DOX+(DR-DOX)*(1.-SATN/100.0)
      CP=SOL*PCO2*(1.0+10.0**(PHE-PK))
      CCC=DDD*CP
      CO2CON=(HCRIT*CCC*0.01 + (1.0-HCRIT*0.01)*CP)*2.22
      RETURN
      END
      FUNCTION SATURA(PO2,PCO2,PHE)
      COMMON HB,HCRIT,TEMP,APH30,BPH60,APCO2,BPCO2,DP50,PB,PIO2
      A1=-8532.229
      A2=2121.401
      A3=-67.07399
      A4=935960.9
      A5=-31346.26
      A6=2396.167
      A7=-67.10441
      B=0.43429*ALOG(40.0/PCO2)
      X=PO2*10.0**(0.024*(37.0-TEMP)+0.4*(PHE-7.4)+0.06*B)
      X=26.8*X/(26.8+DP50)
      IF(X-10.0) 10,20,20
  10  SATURA=0.3683*X + 0.0584*X*X
      RETURN  
  20  SATURA=100.0*(X*(X*(X*(X+A3)+A2)+A1))/(X*(X*(X*(X+A7)+A6)+A5)+A4)
      RETURN
      END